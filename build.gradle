//apply plugin: "ca.coglinc.javacc"
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'war'

group = 'org.ow2.proactive'
version = version

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "ca.coglinc:javacc-gradle-plugin:2.3.1"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
    }
}

bootRepackage {
    enabled = false
}

configurations {
    all*.exclude module: 'spring-boot-starter-logging'

    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

//compileJavacc {
//    inputDirectory = file('src/main/javacc')
//    outputDirectory = file(project.buildDir.absolutePath + '/generated/javacc')
//}

//compileJjtree {
//    inputDirectory = file('src/main/jjtree')
//    outputDirectory = file(project.buildDir.absolutePath + '/generated/jjtree')
//    arguments = [grammar_encoding: 'UTF-8', static: 'false']
//}

coveralls {
    sourceDirs = allprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
}

//jjdoc {
//    inputDirectory = file('src/main/javacc')
//    outputDirectory = file(project.buildDir.absolutePath + '/generated/jjdoc')
//    arguments = [text: 'true']
//}

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }

    // Below is a trick for IntelliJ IDEA to add integration test
    // resources to classpath when running integration tests from
    // the IDE. It's is not a good solution but I don't know about
    // a better one.
    test {
        resources.srcDir file('src/integration-test/resources')
    }
}

springBoot {
    mainClass = 'org.ow2.proactive.workflow_catalog.rest.Application'
}

war {
    baseName = "workflow-catalog"
    version = version
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.3.1.RELEASE'
    compile('org.springframework.boot:spring-boot-starter-web:1.3.1.RELEASE') {
        exclude module: "spring-boot-starter-tomcat"
        exclude module: "spring-boot-starter-validation"
    }

    compile 'org.springframework.boot:spring-boot-legacy:1.0.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-log4j2:1.3.1.RELEASE'

    providedRuntime 'com.google.guava:guava:19.0'
    providedRuntime 'org.hsqldb:hsqldb:2.3.3'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat:1.3.1.RELEASE'

    compile 'io.springfox:springfox-spring-web:2.3.0'
    compile 'io.springfox:springfox-swagger2:2.3.0'
    compile 'io.springfox:springfox-swagger-ui:2.3.0'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.28'

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.1.RELEASE'
    testCompile 'com.jayway.restassured:rest-assured:2.8.0'
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

task integrationTest(type: Test, dependsOn: integrationTestClasses) {
    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDir = sourceSets.integrationTest.output.classesDir

    outputs.upToDateWhen { false }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIntegrationTest.exec")
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    // execution files
    executionData test, integrationTest

    // source sets to report on the coverage of
    sourceSets project.sourceSets.main

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

jacocoTestReport.dependsOn check
tasks.coveralls.dependsOn jacocoTestReport

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        testSourceDirs += file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/resources')

        scopes.TEST.plus += [
                configurations.integrationTestCompile,
                configurations.integrationTestRuntime
        ]
    }
}