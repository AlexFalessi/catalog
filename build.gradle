apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'
apply plugin: 'war'

group = 'org.ow2.proactive'
version = version

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
    }
}

bootRepackage {
    mainClass = 'org.ow2.proactive.workflow_catalog.rest.Application'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }

    // Below is a trick for IntelliJ IDEA to add integration test
    // resources to classpath when running integration tests from
    // the IDE. It's is not a good solution but I don't know about
    // a better one.
    test {
        resources.srcDir file('src/integration-test/resources')
    }
}

war {
    baseName = "workflow-catalog"
    version = version
}

dependencies {
    compile 'com.google.guava:guava:19.0'

    compile 'org.springframework.boot:spring-boot-starter-web:1.3.1.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.3.1.RELEASE'

    runtime 'org.hsqldb:hsqldb:2.3.3'

    compile 'io.springfox:springfox-spring-web:2.3.0'
    compile 'io.springfox:springfox-swagger2:2.3.0'
    compile 'io.springfox:springfox-swagger-ui:2.3.0'

    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.28'

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.1.RELEASE'
    testCompile 'com.jayway.restassured:rest-assured:2.8.0'
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    }
}

task integrationTest(type: Test, dependsOn: integrationTestClasses) {
    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDir = sourceSets.integrationTest.output.classesDir

    outputs.upToDateWhen { false }

    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoIntegrationTest.exec")
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

task jacoco(type: JacocoReport) {
    // execution files
    executionData test, integrationTest

    // source sets to report on the coverage of
    sourceSets project.sourceSets.main

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.coveralls {
    dependsOn 'check'
    onlyIf { isCI }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        testSourceDirs += file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/resources')

        scopes.TEST.plus += [
                configurations.integrationTestCompile,
                configurations.integrationTestRuntime
        ]
    }
}
